pluginManagement {
	includeBuild "gradle/plugins/lifecycle-smoke-test-plugin"
	repositories {
		gradlePluginPortal()
		maven {
			url "https://repo.spring.io/snapshot"
		}
		maven {
			url 'https://repo.spring.io/milestone'
		}
		maven {
			url "https://repo.spring.io/release"
			content {
				includeGroup "io.spring.ge.conventions"
			}
		}
	}
}

plugins {
	id "com.gradle.develocity" version "3.17.3"
	id "io.spring.ge.conventions" version "0.0.17"
}

rootProject.name="spring-lifecycle-smoke-tests"

include "lifecycle-smoke-test-support"
include "lifecycle-listener"

["boot", "cloud", "data", "framework", "integration", "security", "session"].each { group ->
	file(group).eachDirMatch(~/[a-z].*/) { smokeTest ->
		include "$group:${smokeTest.name}"
	}
}

develocity {
	buildScan {
		value("Spring Boot generation", "${springBootGeneration}")
	}
}
